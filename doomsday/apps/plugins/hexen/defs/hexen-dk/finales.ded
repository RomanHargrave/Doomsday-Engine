# Hexen (DK) - Intermissions and Finales
#
# For further information on the InFine system, script syntax
# and semantics see:
#
#   http://dengine.net/dew/?title=InFine_script_reference
#

Header { Version = 5; }

# I'm using "textdef" to allow compatibility with DeHackEd.
# A better way would just be to use "text" and define the text
# in the script, no need to go about modifying the Text defs.
#
# Notes:
# Deathkings uses maps 41-60.

# 1st Hub
Finale
{
  After = "MAP42";
  Script
  {
    # Only show this after the hub has been completed.
    ifnot leavehub end;
    # Don't show this in deathmatch.
    if deathmatch end;
    music "hub";
    image back INTERPIC;
    wait 0.5; # Originally 10 tics.
    textlump msg 10 5 CLUS1MSG;
    #>text msg 10 5
      "wiping a trembling hand across your\n
      bleeding face, you try to clear\n
      your mind for what lies ahead...\n\n
      ...and forget what lies behind.\n\n
      in the distance, the stark ramparts\n
      of a great castle complex seem to\n
      rend the sky above, and the stench\n
      of decay wafts from the violated\n
      graves of uncounted dead.\n\n
      carefully counting what little\n
      remains of your artifacts, you try\n
      to reassure yourself that it will\n
      be enough. after all, it has to be\n
      enough, doesn't it?\n\n\n
      doesn't it?";<#
    waittext msg;
    wait 5; # Original TEXTWAIT is 140+10 tics.
  };
}

# 2nd Hub
Finale
{
  After = "MAP48";
  Script
  {
    # Only show this after the hub has been completed.
    ifnot leavehub end;
    # Don't show this in deathmatch.
    if deathmatch end;
    music "hub";
    image back INTERPIC;
    wait 0.5; # Originally 10 tics.
    textlump msg 10 5 CLUS2MSG;
    #>text msg 10 5
      "surely the souls of the damned inhabit\n
      this world, for nothing fair or good\n
      could survive here for long.\n\n
      but what has passed before can only\n
      be a pale shadow of what bars your\n
      passage now: the dark citadel itself.\n\n
      the grim bulk of the cathedral blocks\n
      all but fragmentary glimpses of the\n
      citadel proper, but what can be seen\n
      speaks in sibilant whispers of cold,\n
      lingering death...\n\n\n\n\n\n
      ...for the fortunate.";<#
    waittext msg;
    wait 5; # Original TEXTWAIT is 140+10 tics.
  };
}

# 3rd Hub
Finale
{
  After = "MAP60";
  Script
  {
    # This is the real finale of the game.
    noskip; # No more levels after this, disallow skipping.
    musiconce "hall";
    # Setup the first screen.
    image back FINALE1;
    # Fade into the first screen.
    filter 0 0 0 1; in 2 filter 0 0 0 0; wait 2;
    textlump msg 20 5 WIN1MSG;
    #>text msg 20 5
      "once again you find yourself in the\n
      great hall of the chaos sphere, as\
      if no time had passed from when\n
      last you moved among these shadows.\n\n
      but something is eerily different,\n
      a silence where once had been soft\n
      whispers, a sense of being watched\n
      by hidden eyes...\n\n
      ...eyes which shield a malefic\n
      intent.";<#
    waittext msg;
    wait 7; # Original TEXTWAIT is 250 tics.

    # Second screen: the Orb.
    musiconce "orb";
    image back FINALE2;
    deltext msg;
    wait 0.5;
    textlump msg 20 5 WIN2MSG;
    #>text msg 20 5
      "once before you grasped the chaos\n
      sphere, held it within trembling\n
      hands. now your hands tremble with\n
      something more than avarice, and\n
      dread meshes with the hunger for\n
      power.\n\n
      if even the power of the sphere is\n
      not enough to protect you from the\n
      forces of darkness, perhaps it is\n
      better left untouched, its promise\n
      left unkept.\n\n\n\n
      but then, you never were one to\n
      back down from a challenge...";<#
    waittext msg;
    wait 7; # Original TEXTWAIT is 250 tics.
    # Fade out.
    in 2 filter 0 0 0 1; wait 2;
    deltext msg;

    # Setup the last screen: the Chess.
    music "chess";
    image back FINALE3;
    # Choose the right player patch (no patch=fighter).
    if netgame patch pl 20 0 CHESSALL;
    if netgame goto m;
    if cleric patch pl 60 0 CHESSC;
    if mage patch pl 60 0 CHESSM;
    marker m;
    # Fade in.
    filter 0 0 0 0; wait 2;
    #text msg 20 135 "\n...and other players await.";
    textlump msg 20 135 WIN3MSG;
  };
}
