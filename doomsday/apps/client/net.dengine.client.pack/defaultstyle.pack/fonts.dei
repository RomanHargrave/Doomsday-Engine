# Fonts for the default UI style
#
# - family: font family name
# - size can be "pt" or "px", defaults to "pt" 
# - weight: normal, bold, light
# - style: normal, italic
# - transform: normal, uppercase, lowercase

script { 
    import Version, gui 
    # Load the Open Sans font contained in this pack.
    def loadOpenSans()
        import App
        fontDir = __file__.fileNamePath() / "fonts"
        # Sadly, light fonts seem to not work in Qt presently.
        for style in ['Regular', 'Bold', 'Italic', 'BoldItalic']  #'Light', 'LightItalic'
            App.loadFont(fontDir / ("OpenSans-%s.ttf" % style))
        end    
    end
}

group {
    condition: Version.OS == 'windows'
    
    script { loadOpenSans() }

    font default {
        family: Open Sans
        size: 12pt
        weight: normal
        style: normal    
    }
    
    font monospace inherits default {
        family: Courier New
        size: 10pt
    }
}

group {
    condition: Version.OS == 'macx'

    script {
        # Define mappings for native font styles and weights.
        import App, DisplayMode
        App.addFontMapping("Helvetica Neue", {
            ['regular', 25]: 'HelveticaNeue-Light',
            ['regular', 50]: 'HelveticaNeue',
            ['regular', 75]: 'HelveticaNeue-Bold',
            ['italic',  25]: 'HelveticaNeue-LightItalic',
            ['italic',  50]: 'HelveticaNeue-Italic',
            ['italic',  75]: 'HelveticaNeue-BoldItalic'
        })
        App.addFontMapping("Menlo", {
            ['regular', 25]: 'Menlo-Regular',
            ['regular', 50]: 'Menlo-Regular',
            ['regular', 75]: 'Menlo-Bold',
            ['italic',  25]: 'Menlo-Italic',
            ['italic',  50]: 'Menlo-Italic',
            ['italic',  75]: 'Menlo-BoldItalic'
        })
    }
    
    font default {
        family: Helvetica Neue
        size $: gui.scale('16pt', DisplayMode.DPI_FACTOR)
        weight: normal
        style: normal
    }
    
    font monospace inherits default {
        family: Menlo
        size $: gui.scale('13pt', DisplayMode.DPI_FACTOR)
    }
}

group {
    condition: Version.OS == 'unix'

    font default {
        family: Liberation Sans
        size: 13pt
        weight: normal
        style: normal
    }

    font monospace inherits default {
        family: FreeMono
        size: 12pt
    }
}

font title inherits default {
    size $: gui.scale(self.size, 1.75)
    weight: light
}

font heading inherits title {
    size $: gui.scale(default.size, 1.2)
}

font small inherits default {
    size $: gui.scale(self.size, 0.75)
}

group editor {
    font plaintext inherits default {}
    font hint inherits default {
        style: italic
        weight: light
    }
}

group separator {
    font empty inherits default {
        size $: gui.scale(self.size, 0.5)
    }
    font label inherits default {
        condition: Version.OS == 'macx'
        size $: gui.scale(self.size, 0.7)
        transform: uppercase
    }
    font label inherits small {
        condition: Version.OS != 'macx'
        weight: bold
    }
    font annotation inherits small {}    
}

group choice {
    font selected inherits default {
        weight: bold
    }
}

group tab {
    font label inherits title {
        size $: gui.scale(self.size, 0.75)
    }
    font selected inherits tab.label {}
}

group slider {
    font label inherits small {}
    font value inherits slider.label {
        weight: bold
    }
}

group log {
    font normal inherits default {}
}
